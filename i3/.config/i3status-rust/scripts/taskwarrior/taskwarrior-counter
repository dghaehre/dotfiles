#!/usr/bin/env janet
(use sh)

(defn display-today [done todo]
  (let [donebar     (string/repeat "▰" done) 
        notdonebar  (string/repeat "▱" todo)]
    (string donebar notdonebar "  ")))

(def context-grammar
  (peg/compile ~(* :a* :s* "'" (capture :a* :context) "'")))

(defn get-context []
  (let [s ($< task context show)
        c (-> (peg/match context-grammar s)
              (get 0))]
    (if (nil? c) "" (string "@" c))))

(defn to-number [s]
  (try
    (-> (string/trim s)
        (int/u64)
        (int/to-number))
    ([_] 0)))

# We are currently not using today-bar
# This needs refinement if we are going to use it.
(defn main [& args]
  (let [todos     (-> ($< task status:pending or status:waiting count) (to-number))
        today     (-> ($< task status:completed end.after:today count) (to-number))
        ready     (-> ($< task +READY count) (to-number) (min 20))
        done      (-> ($< task status:completed count) (to-number))
        context   (get-context)
        today-bar (display-today today ready)]
    (prin (string context " " today-bar "  ☐ " todos " ■ " done))))
