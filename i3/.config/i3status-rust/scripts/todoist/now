#!/usr/bin/env janet
(import sh)
(import json)

(defn read-from-file [file-path]
  (let [f         (file/open file-path :r)
        content   (file/read f :all)]
    (file/close f)
    content))

(defn setup []
  (let [path  (string (os/getenv "HOME") "/.todoist.token")
        token (-> (read-from-file path) (string/trim))]
    (setdyn :todoist/token token)))

(defn token []
  (dyn :todoist/token))

(defn get-todoist [path]
  (let [auth (string "Authorization: Bearer " (token))
        res (sh/$< curl -s -X GET ,path  -H ,auth)]
    (json/decode res)))

(defn get-now-label []
  (as-> (get-todoist "https://api.todoist.com/rest/v2/tasks?filter=@now") _
        (map |(get $ "content") _)
        (string/join _ ", ")))

(comment
  (get-now-label))

(defn display [l]
  (if (nil? l)
    (json/encode {:icon "headphones" :text "" :state "Info"})
    (json/encode {:icon "headphones" :text l :state "Info"})))

(defn main [& args]
  (setup)
  (try
    (-> (get-now-label)
        (display)
        (prin))
    ([_] (prin (display "")))))
